$(document).on 'turbolinks:load', ->
  if document.getElementById('nrdb_decks')? || document.getElementById('display_decks')? || document.getElementById('display_opponent_deck')?

    nrdbPrintingsById = new Map()

    window.getNrdbPrinting = (printingId) =>
      nrdbPrintingsById.get(printingId)

    window.loadNrdbPrintings = (printingIds, callback) =>
      printingIds = printingIdsNotInMap(printingIds)
      if printingIds.length == 0
        callback()
        return

      loadNrdbPrintings callback

    window.searchNrdbCards = (query, callback) =>
      getPrintings({
        data: {
          'filter[search]': query,
          'filter[distinct_cards]': '',
          'page[size]': 10000
        },
        success: (response) =>
          for nrdbPrinting from response.data
            nrdbPrintingsById.set(nrdbPrinting.id, nrdbPrinting)
          callback(response.data)
      })

    printingIdsNotInMap = (printingIds) =>
      set = new Set(printingIds)
      for printingId from set
        if nrdbPrintingsById.has(printingId)
          set.delete(printingId)
      Array.from(set)

    loadNrdbPrintings = (callback) =>
      getPrintings({
        data: {
          'page[size]': 10000
        },
        success: (response) =>
          for nrdbPrinting from response.data
            nrdbPrintingsById.set(nrdbPrinting.id, nrdbPrinting)
          callback()
      })

    getPrintings = (request) =>
      request.data['fields[printings]'] = 'card_id,card_type_id,title,side_id,faction_id,minimum_deck_size,influence_limit,influence_cost'
      $.get({
        url: '<%= Rails.configuration.nrdb_api_host %>/api/v3/public/printings',
        data: request.data,
        success: request.success
      })
